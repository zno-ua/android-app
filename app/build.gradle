apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "net.zno_ua.app"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 200
        versionName "2.0"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("release.jks")
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " debug"
            signingConfig signingConfigs.debug
            ext.enableCrashlytics = false
            resValue "string", "BOTTOM_BANNER_UNIT_ID", "ca-app-pub-3940256099942544/6300978111"
            resValue "string", "INTERSTITIAL_UNIT_ID", "ca-app-pub-3940256099942544/1033173712"
        }

        all {
            buildConfigField "String", "SERVER_URL", SERVER_URL
            buildConfigField "int", "API_VERSION", API_VERSION
            buildConfigField "String", "API_KEY", API_KEY
        }

        release {
            signingConfig signingConfigs.release
            ext.enableCrashlytics = true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            resValue "string", "BOTTOM_BANNER_UNIT_ID", BOTTOM_BANNER_UNIT_ID
            resValue "string", "INTERSTITIAL_UNIT_ID", INTERSTITIAL_UNIT_ID
        }
    }
}

ext.supportLibraryVersion = "23.2.1"
ext.playServicesVersion = "8.4.0"

dependencies {
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    compile "com.android.support:percent:${supportLibraryVersion}"
    compile "com.android.support:customtabs:${supportLibraryVersion}"
    compile "com.google.android.gms:play-services-gcm:${playServicesVersion}"
    compile "com.google.android.gms:play-services-analytics:${playServicesVersion}"
    compile "com.google.android.gms:play-services-ads:${playServicesVersion}"
    compile('com.github.afollestad.material-dialogs:core:0.8.5.6@aar') { transitive = true }
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-jackson:2.0.0-beta4'
    compile 'com.squareup.okhttp3:logging-interceptor:3.1.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'org.adw.library:discrete-seekbar:1.0.0'
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:1.0.5'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') { transitive = true }
}

apply plugin: 'com.google.gms.google-services'